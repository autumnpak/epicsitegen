file = _{ SOI ~ ast ~ EOI }
ws = _{ (" "|"\t"|"\n"|"\r")+ }
ast = { (replacement | snippet | file_element | if_exists | for_element | plain_text)* }
value = { ident ~ (field | index)* }
ident = { ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "-" | "_")+ }
field = { "." ~ ident }
index = { "[" ~ numbers ~ "]" }
numbers = { ASCII_DIGIT+ }
replacement = { "{{" ~ ws? ~ value ~ ws? ~ pipes ~ "}}" }
plain_text = { not_open_brace+ | ("{" ~ not_second_character+) }
not_open_brace = { !"{" ~ ANY }
not_second_character = { !("{" | "%") ~ ANY }
not_pipe_end = { !(" " | "}" | "|") ~ ANY }
snippet = { "{%" ~ ws? ~ "snippet" ~ ws? ~ (file_at | filename) ~ ws? ~ pipes ~ "%}" }
file_element = { "{%" ~ ws? ~ "file" ~ ws? ~ (file_at | filename) ~ ws? ~ pipes ~ "%}" }
filename = { (!" " ~ ANY)+ }
file_at = { "@" ~ ws? ~ value }
pipes = { ("|" ~ ws? ~ pipe )* }
pipe = { pipe_name ~ (ws? ~ not_pipe_end+)* ~ ws? }
pipe_name = { not_pipe_end+ }
if_exists = { "{%" ~ ws? ~ "if-exists" ~ ws ~ value ~ ws? ~ "%}" ~ ast ~ if_exists_else? ~ if_exists_end }
if_exists_else = { "{%" ~ ws? ~ "else" ~ ws? ~ "%}" ~ ws? ~ ast }
if_exists_end = { "{%" ~ ws? ~ "endif" ~ ws? ~ "%}" }
for_element = { "{%" ~ ws? ~ "for" ~ ws ~ ident ~ ws ~ "in" ~ ws ~ value ~ ws? ~ "%}" ~ ast ~ for_sep? ~ for_end }
for_sep = { "{%" ~ ws? ~ "sep" ~ "erator"? ~ ws? ~ "%}" ~ ws? ~ ast }
for_end = { "{%" ~ ws? ~ "endfor" ~ ws? ~ "%}" } 
